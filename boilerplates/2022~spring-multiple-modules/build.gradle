import com.github.spotbugs.SpotBugsTask

buildscript {
    repositories {
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.24.1"
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:2.0.0"
    }
}

subprojects {
    apply plugin: "java"
    // api configuration support
    apply plugin: "java-library"
    apply plugin: "idea"
    apply plugin: "com.github.spotbugs"
    apply plugin: "com.diffplug.gradle.spotless"
    apply plugin: "maven"

    group = 'wx'
    version = '1.0-SNAPSHOT'

    sourceCompatibility = 1.8

    repositories {
        mavenCentral()
        // 自定义 Nexus 服务器
        //        maven {
        //            url = uri("${nexusUrl}/repository/maven-snapshots/")
        //            credentials {
        //                username "${nexusSnapshotUsername}"
        //                password "${nexusSnapshotPassword}"
        //            }
        //        }
    }

    dependencies {
        implementation "com.google.guava:guava:${guavaVersion}"
        implementation "org.slf4j:slf4j-api:${slf4jVersion}"
        implementation "io.reactivex.rxjava3:rxjava:${rxJavaVersion}"

        testImplementation "org.junit.jupiter:junit-jupiter-api:${jupiterVersion}"
        testImplementation "org.junit.jupiter:junit-jupiter-params:${jupiterVersion}"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${jupiterVersion}"
        testImplementation "org.hamcrest:hamcrest-library:${hamcrestLibraryVersion}"

        compileOnly "org.jetbrains:annotations:${jetbrainsAnnotationVersion}"
        compileOnly "com.google.code.findbugs:annotations:${findbugsAnnotationVersion}"
        compileOnly "org.projectlombok:lombok:${lombokVersion}"
        annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
        testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
        testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    }

    // Disable JavaDoc doclint on Java 8. It's annoying.
    if (JavaVersion.current().isJava8Compatible()) {
        allprojects {
            tasks.withType(Javadoc) {
                options.addStringOption('Xdoclint:none', '-quiet')
            }
        }
    }

    spotbugs {
        includeFilter = file("$rootDir/buildscripts/spotbugs-filter.xml")
    }

    tasks.withType(SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    spotless {
        format 'misc', {
            target '**/*.gradle', '**/.gitignore'

            trimTrailingWhitespace()
            endWithNewline()
        }
        java {
            removeUnusedImports()
            googleJavaFormat()
        }
        freshmark {
            target '**/*.md'
        }
    }
}
